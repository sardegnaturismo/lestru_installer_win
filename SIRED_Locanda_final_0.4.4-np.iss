; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Lestru"
#define MyAppVersion "0.4.4"
#define MyAppPublisher "Regione Autonoma Sardegna"
#define MyAppURL "http://localhost:8080/locanda/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{1C7D7A0F-159B-4109-8AB3-82160581D73C}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName=Lestru
OutputBaseFilename=Lestru_x64_0.4.4
Compression=lzma
SolidCompression=yes
SetupLogging=yes
;SetupIconFile=C:\Users\WellnetWindows\Desktop\locanda-installer-64\lestru.ico
LicenseFile="setup.project\licenses\licence.txt"
WindowVisible=yes 
WindowResizable=yes
DisableDirPage=yes

[Languages]
Name: "italian"; MessagesFile: "compiler:Languages\Italian.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{code:GetAppURL}"; IconFilename: C:\Users\WellnetWindows\Desktop\locanda-installer-64\lestru.ico
Name: {group}\{cm:UninstallProgram,{#MyAppName}}; Filename: {uninstallexe}; IconFilename: C:\Users\WellnetWindows\Desktop\locanda-installer-64\lestru.ico
Name: "{commondesktop}\{#MyAppName}"; Filename: "{code:GetAppURL}"; Tasks: desktopicon; IconFilename: C:\Users\WellnetWindows\Desktop\locanda-installer-64\lestru.ico


[Files]
Source: "setup.project\tomcat\*"; DestDir: "{code:GetDirAct}\tmp\tomcat"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "setup.project\locanda\*"; DestDir: "{code:GetDirAct}\tmp\locanda"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "setup.project\java\*"; DestDir: "{code:GetDirAct}\tmp\java"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "setup.project\facilities\*"; DestDir: "{code:GetDirAct}\tmp\facilities"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "setup.project\info\*"; DestDir: "{code:GetDirAct}\tmp\info"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "setup.project\licenses\*"; DestDir: "{code:GetDirAct}\tmp\licenses"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "setup.project\installer\*"; DestDir: "{code:GetDirAct}\tmp\installer"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "setup.project\installer\*"; DestDir: "{code:GetDirAct}\installer"; Flags: ignoreversion createallsubdirs recursesubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Dirs]
Name: "{code:getDirAct}"; Permissions: users-full

[Run]
Filename: "{code:GetDirAct}\installer\automation\automationStop.bat"; WorkingDir: "{code:GetDirAct}\installer\automation"; Flags: runhidden; StatusMsg: "Stop del server. L'operazione potrebbe richiedere qualche minuto..."; 
Filename: "{code:GetDirAct}\installer\automation\automationRun.bat"; WorkingDir: "{code:GetDirAct}\installer\automation"; Flags: runhidden; StatusMsg: "Avvio del server. L'operazione potrebbe richiedere qualche minuto..."; 
Filename: "{code:GetDirAct}\tomcat\bin\serviceUninstall.bat"; WorkingDir: "{code:GetDirAct}\tomcat\bin"; Flags: runhidden; StatusMsg: "Aggiornamento servizi in corso..."; Check: CheckServiceUninstall;
Filename: "{code:GetDirAct}\tomcat\bin\serviceInstallAuto.bat"; WorkingDir: "{code:GetDirAct}\tomcat\bin"; Flags: runhidden; StatusMsg: "Aggiornamento servizi in corso..."; Check: CheckServiceInstallAuto;
Filename: "{code:GetDirAct}\installer\automation\automationClean.bat"; WorkingDir: "{code:GetDirAct}\installer\automation"; Flags: runhidden; StatusMsg: "Cancellazione file temporanei in corso..."; Check: CheckCleanAllow;
Filename: "{code:GetDirAct}\installer\automation\startService.bat"; WorkingDir: "{code:GetDirAct}\installer\automation"; Flags: runhidden; StatusMsg: "Cancellazione file temporanei in corso..."; Check: CheckCleanAllow;
Filename: "{code:GetAppURL}"; Flags: shellexec postinstall skipifsilent; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Check: CheckServiceInstallAuto;

[UninstallRun]
Filename: "{code:GetDirAct}\tomcat\bin\serviceUninstall.bat"; WorkingDir: "{code:GetDirAct}\tomcat\bin"; Flags: runhidden;

[Registry]
Root: HKCU; Subkey: "Software\{#MyAppPublisher}"; Flags: uninsdeletekeyifempty createvalueifdoesntexist
Root: HKCU; Subkey: "Software\{#MyAppPublisher}\{#MyAppName}"; Flags: uninsdeletekey createvalueifdoesntexist
Root: HKCU; Subkey: "Software\{#MyAppPublisher}\{#MyAppName}\Settings"; ValueType: string; ValueName: "Dir"; ValueData: "{app}" 
Root: HKCU; Subkey: "Software\{#MyAppPublisher}\{#MyAppName}\Settings"; ValueType: string; ValueName: "Version"; ValueData: "{#MyAppVersion}" 
Root: HKCU; Subkey: "Software\{#MyAppPublisher}\{#MyAppName}\Settings"; ValueType: string; ValueName: "SetupType"; ValueData: "{code:GetSetupType}" 
Root: HKCU; Subkey: "Software\{#MyAppPublisher}\{#MyAppName}\Settings"; ValueType: string; ValueName: "DbType"; ValueData: "{code:GetDbType}" 
Root: HKCU; Subkey: "Software\{#MyAppPublisher}\{#MyAppName}\Settings"; ValueType: string; ValueName: "DbHost"; ValueData: "{code:GetDbHost}" 
Root: HKCU; Subkey: "Software\{#MyAppPublisher}\{#MyAppName}\Settings"; ValueType: string; ValueName: "DbPort"; ValueData: "{code:GetDbPort}" 
Root: HKCU; Subkey: "Software\{#MyAppPublisher}\{#MyAppName}\Settings"; ValueType: string; ValueName: "DbName"; ValueData: "{code:GetDbName}" 
Root: HKCU; Subkey: "Software\{#MyAppPublisher}\{#MyAppName}\Settings"; ValueType: string; ValueName: "DbUser"; ValueData: "{code:GetDbUser}" 
Root: HKCU; Subkey: "Software\{#MyAppPublisher}\{#MyAppName}\Settings"; ValueType: string; ValueName: "DbPassword"; ValueData: "{code:GetDbPassword}" 
Root: HKCU; Subkey: "Software\{#MyAppPublisher}\{#MyAppName}\Settings"; ValueType: string; ValueName: "DbPreload"; ValueData: "{code:GetDbPreload}" 
Root: HKCU; Subkey: "Software\{#MyAppPublisher}\{#MyAppName}\Settings"; ValueType: string; ValueName: "WebPort"; ValueData: "{code:GetWebPort}" 
Root: HKCU; Subkey: "Software\{#MyAppPublisher}\{#MyAppName}\Settings"; ValueType: string; ValueName: "WebAllowRemoteAccess"; ValueData: "{code:GetWebAllowRemoteAccess}" 
Root: HKCU; Subkey: "Software\{#MyAppPublisher}\{#MyAppName}\Settings"; ValueType: string; ValueName: "WebInstallAsService"; ValueData: "{code:GetWebInstallAsService}" 

[Code]
type
  TDbType = record
    Code: String;
    Desc: String;
  end;
  
  TDbPreload = record
    Code: String;
    Desc: String;
  end;
  
const
  SetKey = 'Software\{#MyAppPublisher}\{#MyAppName}\Settings';

var
  AbortSetup: Boolean;
  IsUpdate: Boolean;
  DirOld: String;
  DirAct: String;
  VersionOld: String;
  VersionAct: String;
  SetupTypeOld: String;
  SetupTypeAct: String;
  DbTypeOld: String;
  DbTypeAct: String;
  DbHostOld: String;
  DbHostAct: String;
  DbPortOld: String;
  DbPortAct: String;
  DbNameOld: String;
  DbNameAct: String;
  DbUserOld: String;
  DbUserAct: String;
  DbPasswordOld: String;
  DbPasswordAct: String;
  DbPreloadOld: String;
  DbPreloadAct: String;
  WebPortOld: String;
  WebPortAct: String;
  WebAllowRemoteAccessOld: String;
  WebAllowRemoteAccessAct: String;
  WebInstallAsServiceOld: String;
  WebInstallAsServiceAct: String;

  DbTypePage: TInputOptionWizardPage;
  DbParamPage1: TInputQueryWizardPage;
  DbParamPage2: TInputQueryWizardPage;
  DbPreloadPage: TInputOptionWizardPage;
  WebParamPage: TInputQueryWizardPage;
  WebOptionPage: TInputOptionWizardPage;
  
  DbTypes: Array[0..3] of TDbType;
  DbPreloads: Array[0..1] of TDbPreload;


function Init(): Boolean;
begin
  
  AbortSetup := False;
 
  DbTypes[0].Code := 'h2';
  DbTypes[0].Desc := 'H2 Database Engine';
  DbTypes[1].Code := 'mysql';
  DbTypes[1].Desc := 'MySQL';
  DbTypes[2].Code := 'postgresql';
  DbTypes[2].Desc := 'PostgreSQL';
  DbTypes[3].Code := 'hsql';
  DbTypes[3].Desc := 'HyperSQL';
  
  DbPreloads[0].Code := 'preload';
  DbPreloads[0].Desc := 'Installa database vuoto con dati di default';
  DbPreloads[1].Code := 'external';
  DbPreloads[1].Desc := 'Utilizza database esistente';
  
  DbTypePage := CreateInputOptionPage(wpSelectComponents,
      'Configurazione Avanzata Database', 'Selezione del tipo di database',
      'Selezionare il tipo di database da utilizzare.'+#13#13+
      'In caso di server esterno, prima di procedere, assicurarsi che il sistema RDBMS sia operativo.'+#13#13+
      'Premere Avanti per continuare.',
      True, False);
  DbTypePage.Add(DbTypes[0].Desc+' (Embedded)');
  DbTypePage.Add(DbTypes[1].Desc+' (Server esterno)');
  //DbTypePage.Add(DbTypes[2].Desc+' (Server esterno)');
  DbTypePage.Add(DbTypes[3].Desc+' (Server esterno)'); 
  DbTypePage.SelectedValueIndex := 0;
    
  DbParamPage1 := CreateInputQueryPage(DbTypePage.ID,
            'Configurazione Avanzata Database', 
            '',
            'Inserire i parametri richiesti e premere Avanti per continuare.');
  DbParamPage1.Add('Host connessione:',False);
  DbParamPage1.Values[0] := '';
  DbParamPage1.Add('Porta connessione:',False);
  DbParamPage1.Values[1] := '';
  DbParamPage1.Add('Nome del database:',False);
  DbParamPage1.Values[2] := '';
  
  DbParamPage2 := CreateInputQueryPage(DbParamPage1.ID,
            'Configurazione Avanzata Database', 
            '',
            'Inserire i parametri richiesti e premere Avanti per continuare.');
  DbParamPage2.Add('Utente connessione:',False);
  DbParamPage2.Values[0] := '';
  DbParamPage2.Add('Password connessione:',true);
  DbParamPage2.Values[1] := '';
  
  DbPreloadPage := CreateInputOptionPage(DbParamPage2.ID,
      'Configurazione Avanzata Database', 'Selezione configurazione dati',
      'Selezionare la configurazione dati per l''installazione.'+#13#13+
      'Premere Avanti per continuare.',
      True, False);
  DbPreloadPage.Add(DbPreloads[0].Desc);
  DbPreloadPage.Add(DbPreloads[1].Desc);
  DbPreloadPage.SelectedValueIndex := 0;
        
  WebParamPage := CreateInputQueryPage(DbPreloadPage.ID,
           'Configurazione Avanzata Application Server', 
           'Configurazione parametri per Apache Tomcat',
           'Inserire i parametri richiesti e premere Avanti per continuare.');
  WebParamPage.Add('Porta accesso web:',False);
  WebParamPage.Values[0] := '8080';
  
  WebOptionPage := CreateInputOptionPage(WebParamPage.ID,
           'Configurazione Avanzata Application Server', 
           'Configurazione parametri per il server Apache Tomcat',
           'Inserire i parametri richiesti e premere Avanti per continuare.',
           False, False);
  WebOptionPage.Add('Accetta connessioni web dall''esterno');
  WebOptionPage.Add('Avvia automaticamente il servizio');
  WebOptionPage.Values[0] := False;
  WebOptionPage.Values[1] := True;
      
  Result := True;
end;

function IsNumeric(Value: String): Boolean;
var  
  i: Integer;
  r: Boolean;
  str: String;

begin
  str := '0123456789';
  r := True;

  if (Value = '') then begin
    Result := False;
    Exit;
  end;
                
  r := True;
  for i := 1 to Length(Value) do
  begin
    if (0 = Pos(Value[i],str)) then
      r := False;
  end;

  Result := r;
  
end;

function BoolToStr(Val : Boolean): String;
begin
  if val = True then
    Result := 'True'
  else
    Result := 'False';
end;
  
function LoadInstalled(): Boolean;
var
    Res: Boolean;

begin
  Res := True;

  DirOld := '';
  VersionOld := '';
  SetupTypeOld := '';
  DbTypeOld := '';
  DbHostOld := '';
  DbPortOld := '';
  DbNameOld := '';
  DbUserOld := '';
  DbPasswordOld := '';
  DbPreloadOld := '';
  WebPortOld := '';
  WebAllowRemoteAccessOld := '';
  WebInstallAsServiceOld := '';

  Res := Res and RegQueryStringValue(HKCU, SetKey, 'Dir', DirOld)
  Res := Res and RegQueryStringValue(HKCU, SetKey, 'Version', VersionOld)
  Res := Res and RegQueryStringValue(HKCU, SetKey, 'SetupType', SetupTypeOld)
  Res := Res and RegQueryStringValue(HKCU, SetKey, 'DbType', DbTypeOld)
  Res := Res and RegQueryStringValue(HKCU, SetKey, 'DbHost', DbHostOld)
  Res := Res and RegQueryStringValue(HKCU, SetKey, 'DbPort', DbPortOld)
  Res := Res and RegQueryStringValue(HKCU, SetKey, 'DbName', DbNameOld)
  Res := Res and RegQueryStringValue(HKCU, SetKey, 'DbUser', DbUserOld)
  Res := Res and RegQueryStringValue(HKCU, SetKey, 'DbPassword', DbPasswordOld)
  Res := Res and RegQueryStringValue(HKCU, SetKey, 'DbPreload', DbPreloadOld)
  Res := Res and RegQueryStringValue(HKCU, SetKey, 'WebPort', WebPortOld)
  Res := Res and RegQueryStringValue(HKCU, SetKey, 'WebAllowRemoteAccess', WebAllowRemoteAccessOld)
  Res := Res and RegQueryStringValue(HKCU, SetKey, 'WebInstallAsService', WebInstallAsServiceOld)

  Result := Res;
end;

function CheckInstalled(): Boolean;
var
    Installed: Boolean; 
    ReturnPage: TOutputMsgWizardPage;

begin
    Installed := True;

    if (CompareStr(DirOld,'')=0) then
      Installed := False;
    
    if (not DirExists(DirOld)) then
      Installed := False;
    
    if (Installed = True) then
        CreateOutputMsgPage(wpWelcome,
        'Controllo versioni installate', 'Verifica la presenza di versioni installate sul sistema',
        'E'' stata rilevata una installazione attualmente presente sul sistema:'#13#13+
        'Programma: {#MyAppName} '+VersionOld+#13#13+
        'Autore: {#MyAppPublisher}'+#13#13+
        'Cartella installazione: '+DirOld+#13#13#13+
        'Al termine della procedura guidata, verrà effettuato un aggiornamento alla nuova versione del programma.'#13#13+
        'Per effettuare una nuova installazione, premere annulla, disinstallare la versione attualmente installata e ripetere la procedura attuale.'#13#13+
        'Premere avanti per continuare.')
    
    Result := Installed;
end;

function SetParameters(): Boolean;
var
    Res: Boolean;

begin
  Res := True;
  
    DirAct := ExpandConstant('{app}');
    VersionAct := ExpandConstant('{#MyAppVersion}');
    SetupTypeAct := WizardSetupType(False);
    DbTypeAct := DbTypes[DbTypePage.SelectedValueIndex].Code;
    DbHostAct := DbParamPage1.Values[0];
    DbPortAct := DbParamPage1.Values[1];
    DbNameAct := DbParamPage1.Values[2];
    DbUserAct := DbParamPage2.Values[0];
    DbPasswordAct := DbParamPage2.Values[1];
    DbPreloadAct := DbPreloads[DbPreloadPage.SelectedValueIndex].Code;
    WebPortAct := WebParamPage.Values[0];
    WebAllowRemoteAccessAct := BoolToStr(WebOptionPage.Values[0]);
    WebInstallAsServiceAct := BoolToStr(WebOptionPage.Values[1]);
  
    if (IsUpdate) then begin
      DirAct := DirOld;
    end;
  
  Result := Res;
end;

function CreateInstallerIni(): Boolean;
var
  IniPath: String;
  Res: Boolean;
  Action: String;
  DirOldPath: String;
  DirActPath: String;

begin
  IniPath := DirAct+'\installer.ini';
  
  DirOldPath := DirOld;
  StringChangeEx(DirOldPath, '\', '\\', True);
  
  DirActPath := DirAct;
  StringChangeEx(DirActPath, '\', '\\', True);
 
  Res := True;
  if (IsUpdate) then
    Action := 'UPDATE-INSTALL'
  else
    Action := 'FIRST-INSTALL';
  
  Res := Res and SetIniString('ACTION', 'Action', Action, IniPath);
  
  if (DbTypeOld = 'h2') then begin
    DbHostOld := 'localhost';
    DbPortOld := '';
    DbNameOld := 'locanda';
    DbUserOld := 'locanda';
    DbPasswordOld := 'locanda';
  end;

  if (DbTypeAct = 'h2') then begin
    DbHostAct := 'localhost';
    DbPortAct := '';
    DbNameAct := 'locanda';
    DbUserAct := 'locanda';
    DbPasswordAct := 'locanda';
  end;

  Res := Res and SetIniString('PREVIOUS', 'DirOld', DirOldPath, IniPath);
  Res := Res and SetIniString('PREVIOUS', 'VersionOld', VersionOld, IniPath);
  Res := Res and SetIniString('PREVIOUS', 'SetupTypeOld', SetupTypeOld, IniPath);
  Res := Res and SetIniString('PREVIOUS', 'DbTypeOld', DbTypeOld, IniPath);
  Res := Res and SetIniString('PREVIOUS', 'DbHostOld', DbHostOld, IniPath);
  Res := Res and SetIniString('PREVIOUS', 'DbPortOld', DbPortOld, IniPath);
  Res := Res and SetIniString('PREVIOUS', 'DbNameOld', DbNameOld, IniPath);
  Res := Res and SetIniString('PREVIOUS', 'DbUserOld', DbUserOld, IniPath);
  Res := Res and SetIniString('PREVIOUS', 'DbPasswordOld', DbPasswordOld, IniPath);
  Res := Res and SetIniString('PREVIOUS', 'DbPreloadOld', DbPreloadOld, IniPath);
  Res := Res and SetIniString('PREVIOUS', 'WebPortOld', WebPortOld, IniPath);
  Res := Res and SetIniString('PREVIOUS', 'WebAllowRemoteAccessOld', WebAllowRemoteAccessOld, IniPath);
  Res := Res and SetIniString('PREVIOUS', 'WebInstallAsServiceOld', WebInstallAsServiceOld, IniPath);
  
  Res := Res and SetIniString('ACTUAL', 'DirAct', DirActPath, IniPath);
  Res := Res and SetIniString('ACTUAL', 'VersionAct', VersionAct, IniPath);
  Res := Res and SetIniString('ACTUAL', 'SetupTypeAct', SetupTypeAct, IniPath);
  Res := Res and SetIniString('ACTUAL', 'DbTypeAct', DbTypeAct, IniPath);
  Res := Res and SetIniString('ACTUAL', 'DbHostAct', DbHostAct, IniPath);
  Res := Res and SetIniString('ACTUAL', 'DbPortAct', DbPortAct, IniPath);
  Res := Res and SetIniString('ACTUAL', 'DbNameAct', DbNameAct, IniPath);
  Res := Res and SetIniString('ACTUAL', 'DbUserAct', DbUserAct, IniPath);
  Res := Res and SetIniString('ACTUAL', 'DbPasswordAct', DbPasswordAct, IniPath);
  Res := Res and SetIniString('ACTUAL', 'DbPreloadAct', DbPreloadAct, IniPath);
  Res := Res and SetIniString('ACTUAL', 'WebPortAct', WebPortAct, IniPath);
  Res := Res and SetIniString('ACTUAL', 'WebAllowRemoteAccessAct', WebAllowRemoteAccessAct, IniPath);
  Res := Res and SetIniString('ACTUAL', 'WebInstallAsServiceAct', WebInstallAsServiceAct, IniPath);
  
  Result := Res;
end;

function GetDirAct(Param: String): String;
begin
    Result := DirAct
end;

function GetSetupType(Param: String): String;
begin
    Result := SetupTypeAct
end;

function GetDbType(Param: String): String;
begin
    Result := DbTypeAct
end;

function GetDbHost(Param: String): String;
begin
    Result := DbHostAct
end;
  
function GetDbPort(Param: String): String;
begin
    Result := DbPortAct
end;
 
function GetDbName(Param: String): String;
begin
    Result := DbNameAct
end;

function GetDbUser(Param: String): String;
begin
    Result := DbUserAct
end;
  
function GetDbPassword(Param: String): String;
begin
    Result := DbPasswordAct
end;

function GetDbPreload(Param: String): String;
begin
    Result := DbPreloadAct
end;
  
function GetWebPort(Param: String): String;
begin
    Result := WebPortAct
end;
  
function GetWebAllowRemoteAccess(Param: String): String;
begin
    Result := WebAllowRemoteAccessAct
end;
  
function GetWebInstallAsService(Param: String): String;
begin
    Result := WebInstallAsServiceAct
end;

function GetAppURL(Param: String): String;
begin
    Result := 'http://localhost:'+WebPortAct+'/locanda'
end;

function CheckServiceInstallAuto: Boolean;
var
  AutRes: String;

begin
  AutRes := GetIniString('AUTOMATION', 'AutomationResult', 'failure', DirAct+'\installer.ini'); 
  Result := ((WebInstallAsServiceAct = 'True') and (AutRes = 'success'));
end;

function CheckServiceInstallManual: Boolean;
var
  AutRes: String;

begin
  AutRes := GetIniString('AUTOMATION', 'AutomationResult', 'failure', DirAct+'\installer.ini'); 
  Result := ((WebInstallAsServiceAct = 'False') and (AutRes = 'success'));
end;

function CheckServiceUninstall: Boolean;
var
  AutRes: String;

begin
  AutRes := GetIniString('AUTOMATION', 'AutomationResult', 'failure', DirAct+'\installer.ini'); 
  Result := ((IsUpdate) and (AutRes = 'success'));
end;

function CheckCleanAllow: Boolean;
var
  AutRes: String;

begin
  AutRes := GetIniString('AUTOMATION', 'AutomationResult', 'failure', DirAct+'\installer.ini'); 
  Result := (AutRes = 'success');
end;

function SetAbortSetup(): Boolean;
var 
  Newfilepathname: String;
  LogFileName: String;
  Logfilepathname: String;
  
begin
   Logfilepathname := ExpandConstant('{log}');
   Logfilename := ExtractFileName(Logfilepathname);
   Newfilepathname := DirAct+'\'+Logfilename;
   Filecopy(Logfilepathname, Newfilepathname, False);

   RegWriteStringValue(HKCU, SetKey, 'Dir', DirOld);
   RegWriteStringValue(HKCU, SetKey, 'Version', VersionOld);
   RegWriteStringValue(HKCU, SetKey, 'SetupType', SetupTypeOld);
   RegWriteStringValue(HKCU, SetKey, 'DbType', DbTypeOld);
   RegWriteStringValue(HKCU, SetKey, 'DbHost', DbHostOld);
   RegWriteStringValue(HKCU, SetKey, 'DbPort', DbPortOld);
   RegWriteStringValue(HKCU, SetKey, 'DbName', DbNameOld);
   RegWriteStringValue(HKCU, SetKey, 'DbUser', DbUserOld);
   RegWriteStringValue(HKCU, SetKey, 'DbPassword', DbPasswordOld);
   RegWriteStringValue(HKCU, SetKey, 'DbPreload', DbPreloadOld);
   RegWriteStringValue(HKCU, SetKey, 'WebPort', WebPortOld);
   RegWriteStringValue(HKCU, SetKey, 'WebAllowRemoteAccess', WebAllowRemoteAccessOld);
   RegWriteStringValue(HKCU, SetKey, 'WebInstallAsService', WebInstallAsServiceOld);
   
   AbortSetup := True; 
end;


//***********************************************
//*** NATIVE FUNCTIONS **************************
//***********************************************

procedure InitializeWizard;
begin

  Init();

  LoadInstalled();
  IsUpdate := CheckInstalled();

end;

function ShouldSkipPage(PageID: Integer): Boolean;
begin
  if (PageID = wpSelectDir) then begin
      Result := IsUpdate;
      Exit;
  end;

  if (PageID = wpSelectComponents) then begin
      //Result := IsUpdate;
      //Exit;
  end;
  
  if (PageID = wpSelectProgramGroup) then begin
      //Result := IsUpdate;
      //Exit;
  end;

  if (PageID = wpSelectTasks) then begin
      //Result := IsUpdate;
      //Exit;
  end;

  if (DbTypePage <> nil) then
    if (PageID = DbTypePage.ID) then begin
      //Result := IsUpdate or not IsComponentSelected('db\advanced');
      Result := not IsComponentSelected('db\advanced');
      Exit;
    end;

  if (DbParamPage1 <> nil) then
    if (PageID = DbParamPage1.ID) then begin
      if (DbTypePage <> nil) then
        if (DbTypeAct = 'h2') then begin
          Result := True;
          Exit;
        end;
      
      //Result := IsUpdate or not IsComponentSelected('db\advanced');
      Result := not IsComponentSelected('db\advanced');
      Exit;
    end;

  if (DbParamPage2 <> nil) then
    if (PageID = DbParamPage2.ID) then begin
      if (DbTypePage <> nil) then
        if (DbTypeAct = 'h2') then begin
          Result := True;
          Exit;
        end;
      
      //Result := IsUpdate or not IsComponentSelected('db\advanced');
      Result := not IsComponentSelected('db\advanced');
      Exit;
    end;

  if (DbPreloadPage <> nil) then
    if (PageID = DbPreloadPage.ID) then begin
      
      if (DbTypePage <> nil) then
        //DbTypeAct := DbTypes[DbTypePage.SelectedValueIndex].Code;
        if (DbTypeAct = 'h2') then begin
          Result := True;
          Exit;
        end;
      
      //Result := IsUpdate or not IsComponentSelected('db\advanced');
      Result := not IsComponentSelected('db\advanced');
      Exit;
    end;

  if (WebParamPage <> nil) then
    if (PageID = WebParamPage.ID) then begin
        //Result := IsUpdate or not IsComponentSelected('tomcat\custom');
        Result := not IsComponentSelected('tomcat\custom');
        Exit;
    end;

  if (WebOptionPage <> nil) then
    if (PageID = WebOptionPage.ID) then begin
        //Result := IsUpdate or not IsComponentSelected('tomcat\custom');
        Result := not IsComponentSelected('tomcat\custom');
        Exit;
    end;
  
  if (PageID = wpFinished) then
      Result := AbortSetup;
  
end;

procedure RegisterPreviousData(PreviousDataKey: Integer);
begin
  SetParameters();  
  CreateInstallerIni();  
end;

function NextButtonClick(CurPageID: Integer): Boolean;
var
  I: Integer;
  dbTypeDesc: String;

begin
  Result := True;
  
  if (CurPageID = wpSelectComponents) then begin
    if (IsUpdate) then begin
      if (DbTypeOld = 'h2') then
        DbTypePage.SelectedValueIndex := 0
      if (DbTypeOld = 'mysql') then
        DbTypePage.SelectedValueIndex := 1
      if (DbTypeOld = 'postgresql') then
        DbTypePage.SelectedValueIndex := 2
      if (DbTypeOld = 'hsql') then
        DbTypePage.SelectedValueIndex := 3
     end;

     if (WizardSetupType(False) = 'typical') then begin
        DbTypePage.SelectedValueIndex := 0
     end;
        
  end;

  if (DbTypePage <> nil) then
    if (CurPageID = DbTypePage.ID) then begin
    
      DbTypeAct := DbTypes[DbTypePage.SelectedValueIndex].Code;
      dbTypeDesc := DbTypes[DbTypePage.SelectedValueIndex].Desc;
      
      if (DbTypeAct = '') then begin
          MsgBox('Selezionare un tipo database.', mbError, MB_OK);
          Result := False;
          Exit;
      end;
      
      if (DbTypeAct = 'h2') then
        begin
        
            if ((IsUpdate) and (DbTypeOld = 'h2')) then begin
              DbParamPage1.Values[0] := DbHostOld;
              DbParamPage1.Values[1] := DbPortOld;
              DbParamPage1.Values[2] := DbNameOld;
              DbParamPage2.Values[0] := DbUserOld;
              DbParamPage2.Values[1] := DbPasswordOld;
            end;
 
        end;
    
      if (DbTypeAct = 'mysql') then
        begin
            DbParamPage1.Description := 'Configurazione parametri per il tipo database MySQL'#13#13;
            DbParamPage2.Description := 'Configurazione parametri per il tipo database MySQL'#13#13;
            DbParamPage1.Values[0] := 'localhost';
            DbParamPage1.Values[1] := '3306';
            DbParamPage1.Values[2] := 'locanda';
            DbParamPage2.Values[0] := 'root';
            DbParamPage2.Values[1] := '';
            
            if ((IsUpdate) and (DbTypeOld = 'mysql')) then begin
              DbParamPage1.Values[0] := DbHostOld;
              DbParamPage1.Values[1] := DbPortOld;
              DbParamPage1.Values[2] := DbNameOld;
              DbParamPage2.Values[0] := DbUserOld;
              DbParamPage2.Values[1] := DbPasswordOld;
            end;

            if (DbParamPage1.Values[0] = '') then
              DbParamPage1.Values[0] := '3306';
  
        end;
     
       if (DbTypeAct = 'hsql') then
        begin
            DbParamPage1.Description := 'Configurazione parametri per il tipo database HSQL';
            DbParamPage2.Description := 'Configurazione parametri per il tipo database HSQL';
            DbParamPage1.Values[0] := 'localhost';
            DbParamPage1.Values[1] := '9001';
            DbParamPage1.Values[2] := 'locanda';
            DbParamPage2.Values[0] := 'SA';
            DbParamPage2.Values[1] := '';
            
            if ((IsUpdate) and (DbTypeOld = 'hsql')) then begin
              DbParamPage1.Values[0] := DbHostOld;
              DbParamPage1.Values[1] := DbPortOld;
              DbParamPage1.Values[2] := DbNameOld;
              DbParamPage2.Values[0] := DbUserOld;
              DbParamPage2.Values[1] := DbPasswordOld;
            end;
  
            if (DbParamPage1.Values[0] = '') then
              DbParamPage1.Values[0] := '9001';
  
        end;
     
       if (DbTypeAct = 'postgresql') then
        begin
            DbParamPage1.Description := 'Configurazione parametri per il tipo database PostgreSQL';
            DbParamPage2.Description := 'Configurazione parametri per il tipo database PostgreSQL';
            DbParamPage1.Values[0] := 'localhost';
            DbParamPage1.Values[1] := '5432';
            DbParamPage1.Values[2] := 'locanda';
            DbParamPage2.Values[0] := 'postgres';
            DbParamPage2.Values[1] := '';
            
            if ((IsUpdate) and (DbTypeOld = 'postgresql')) then begin
              DbParamPage1.Values[0] := DbHostOld;
              DbParamPage1.Values[1] := DbPortOld;
              DbParamPage1.Values[2] := DbNameOld;
              DbParamPage2.Values[0] := DbUserOld;
              DbParamPage2.Values[1] := DbPasswordOld;
            end;
        
            if (DbParamPage1.Values[0] = '') then
              DbParamPage1.Values[0] := '5432';
        
        end;
    end;

  if (DbParamPage1 <> nil) then
    if (CurPageID = DbParamPage1.ID) then begin
       if (DbParamPage1.Values[0] = '') then begin
         MsgBox('Inserire l''host.', mbError, MB_OK);
         Result := False;
         Exit;
       end;
       if (not IsNumeric(DbParamPage1.Values[1])) then begin
         MsgBox('Inserire una porta valida.', mbError, MB_OK);
         Result := False;
         Exit;
       end;
       if (DbParamPage1.Values[2] = '') then begin
         MsgBox('Inserire il nome del database.', mbError, MB_OK);
         Result := False;
         Exit;
       end;
  
       if (IsUpdate) then begin
         WebParamPage.Values[0] := WebPortOld;
       end;
    
       if (WebParamPage.Values[0] = '') then
         WebParamPage.Values[0] := '8080';
    end;
  
  if (DbParamPage2 <> nil) then
    if (CurPageID = DbParamPage2.ID) then begin
       if (DbParamPage2.Values[0] = '') then begin
         MsgBox('Inserire l''utente.', mbError, MB_OK);
         Result := False;
         Exit;
       end;
    
       if (IsUpdate) then begin
          if (DbPreloadOld = 'preload') then
            DbPreloadPage.SelectedValueIndex := 0
          if (DbPreloadOld = 'external') then
            DbPreloadPage.SelectedValueIndex := 1
       end;
    end;
   
  if (DbPreloadPage <> nil) then
    if (CurPageID = DbPreloadPage.ID) then begin
      DbPreloadAct := DbPreloads[DbPreloadPage.SelectedValueIndex].Code;
     end;
    
  if (WebParamPage <> nil) then
    if (CurPageID = WebParamPage.ID) then begin
      if (not IsNumeric(WebParamPage.Values[0])) then begin
         MsgBox('Inserire una porta valida.', mbError, MB_OK);
         Result := False;
         Exit;
       end;
    
       if (IsUpdate) then begin
         WebOptionPage.Values[0] := not (WebAllowRemoteAccessOld = 'False');
         WebOptionPage.Values[1] := not (WebInstallAsServiceOld = 'False');
       end;
   
    end;

end;

procedure CurStepChanged(CurStep: TSetupStep);
var
 AutRes, Logfilepathname, Logfilename, Newfilepathname: string;

 begin
    Logfilepathname := ExpandConstant('{log}');
    Logfilename := ExtractFileName(Logfilepathname);
 
  if CurStep = ssInstall then
    begin
      SetParameters();
    end;

  if CurStep = ssDone then
    begin
      Newfilepathname := DirAct+'\'+Logfilename;
      Filecopy(Logfilepathname, Newfilepathname, False);
    end;

  if CurStep=ssPostInstall then
    begin
      AutRes := GetIniString('AUTOMATION', 'AutomationResult', 'failure', DirAct+'\installer.ini'); 
      
      if (not (AutRes = 'success')) then
        begin
          MsgBox('ATTENZIONE: la procedura sarà annullata, a causa di alcuni errori rilevati durante l''installazione.'#13#13+
            'Verificare i files di log nella cartella '#13+
            DirAct+#13' e riavviare il sistema operativo.', mbError, MB_OK);
          SetAbortSetup(); 
        end;
     
    end;

end; 